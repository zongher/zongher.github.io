```js

/******************************************************************************
        DIY Arduino based RC Transmitter
  by Dejan Nedelkovski, www.HowToMechatronics.com
  Library: TMRh20/RF24, https://github.com/tmrh20/RF24/
  作者：HAN
  日期：2020-08-06
//******************************************************************************/
//所需引用的库
#include <U8g2lib.h>

#include <SPI.h>
#include <nRF24L01.h>
#include <RF24.h>
#include <Wire.h>//I2C库

#include "Font.h"
int fontsize = 12;//字体大小
int x[]={0,fontsize,fontsize*2,fontsize*3,fontsize*4,fontsize*5,fontsize*6,fontsize*7,fontsize*8,fontsize*9,fontsize*10};//汉字x坐标
int y[]={0,10,23,36,49,62};//y坐标
//--------------------------------------------------------------------------------------------------
U8G2_SSD1306_128X64_NONAME_1_HW_I2C u8g2(U8G2_R0, /* reset=*/ U8X8_PIN_NONE);

// Define the digital inputs
//定义数字常量
#define jB1 1  // Joystick button 1
#define jB2 0  // Joystick button 2
#define t1 7   // Toggle switch 1
#define t2 4   // Toggle switch 1
#define b1 8   // Button 1
#define b2 9   // Button 2
#define b3 2   // Button 3
#define b4 3   // Button 4
//定义程序变量
const int MPU = 0x68; // MPU6050 I2C address
float AccX, AccY, AccZ;
float GyroX, GyroY, GyroZ;
float roll, pitch, yaw;
float accAngleX, accAngleY, gyroAngleX, gyroAngleY;
float angleX, angleY;
float AccErrorX, AccErrorY, GyroErrorX, GyroErrorY;
float elapsedTime, currentTime, previousTime;
int c = 0;

int screen=1;
int button1_zt=0;//按键状态
int button2_zt=0;//按键状态
int button3_zt=0;//按键状态
int button4_zt=0;//按键状态


//定义radio对象和通讯地址
RF24 radio(5, 6);   // nRF24L01 (CE, CSN)
const byte address[6] = "00001"; // Address

// Max size of this struct is 32 bytes - NRF24L01 buffer limit
struct Data_Package {
  byte j1PotX;
  byte j1PotY;
  byte j1Button;
  byte j2PotX;
  byte j2PotY;
  byte j2Button;
  byte pot1;
  byte pot2;
  byte tSwitch1;
  byte tSwitch2;
  byte button1;
  byte button2;
  byte button3;
  byte button4;
};

Data_Package data; //Create a variable with the above structure

void setup() {
  Serial.begin(9600);

  //初始化OLED12864
  u8g2.begin();



  // Initialize interface to the MPU6050
  //初始化MPU6050
  initialize_MPU6050();

  // Call this function if you need to get the IMU error values for your module
  //计算IMU(惯性测量单元)的误差，这些误差在稍后将会用于计算模块的正确角度
  calculate_IMU_error();

  // Define the radio communication
  //初始化无线通讯
  radio.begin();
  radio.openWritingPipe(address);
  radio.setAutoAck(false);
  radio.setDataRate(RF24_250KBPS);
  radio.setPALevel(RF24_PA_LOW);

  // Activate the Arduino internal pull-up resistors
  //激活数字引脚arduino内部上拉电阻
  pinMode(jB1, INPUT_PULLUP);
  pinMode(jB2, INPUT_PULLUP);
  pinMode(t1, INPUT_PULLUP);
  pinMode(t2, INPUT_PULLUP);
  pinMode(b1, INPUT_PULLUP);
  pinMode(b2, INPUT_PULLUP);
  pinMode(b3, INPUT_PULLUP);
  pinMode(b4, INPUT_PULLUP);

  // Set initial default values
  //设置变量的初始默认值
  data.j1PotX = 127; // Values from 0 to 255. When Joystick is in resting position, the value is in the middle, or 127. We actually map the pot value from 0 to 1023 to 0 to 255 because that's one BYTE value
  data.j1PotY = 127;
  data.j2PotX = 127;
  data.j2PotY = 127;
  data.j1Button = 1;
  data.j2Button = 1;
  data.pot1 = 1;
  data.pot2 = 1;
  data.tSwitch1 = 1;
  data.tSwitch2 = 1;
  data.button1 = 1;
  data.button2 = 1;
  data.button3 = 1;
  data.button4 = 1;




}
void loop() {
  // Read all analog inputs and map them to one Byte value
  //读取所有模拟输入引脚
  //将0-1023的值隐射为0-255的字节值
  data.j1PotX = map(analogRead(A1), 0, 1023, 0, 255);; // Convert the analog read value from 0 to 1023 into a BYTE value from 0 to 255
  data.j1PotY = map(analogRead(A0), 0, 1023, 0, 255);
  data.j2PotX = map(analogRead(A2), 0, 1023, 0, 255);
  data.j2PotY = map(analogRead(A3), 0, 1023, 0, 255);
  data.pot1 = map(analogRead(A7), 0, 1023, 0, 255);
  data.pot2 = map(analogRead(A6), 0, 1023, 0, 255);
  // Read all digital inputs
  data.j1Button = digitalRead(jB1);
  data.j2Button = digitalRead(jB2);
  data.tSwitch1 = digitalRead(t1);
  data.tSwitch2 = digitalRead(t2);
  data.button1 = digitalRead(b1);
  data.button2 = digitalRead(b2);
  data.button3 = digitalRead(b3);
  data.button4 = digitalRead(b4);
  // If toggle switch 1 is switched on
  //开关1使用加速度计和陀螺仪数据
  if (data.tSwitch1 == 0) {
    read_IMU();    // Use MPU6050 instead of Joystick 1 for controling left, right, forward and backward movements
  }
  // Send the whole data from the structure to the receiver
  //发射到接收机
  radio.write(&data, sizeof(Data_Package));

  //OLED12864显示
  oled();

  //按键检测
  button();

}
void button(){

//按钮1
  if(button1_zt==0){
    if(data.button1==0){
        button1_zt=1;
        //Serial.println("[button1]按钮被按下=1");
    }
  }
  if(button1_zt==1){
    if(data.button1==1){
      delay(80);
      if(data.button1==1){
        button1_zt=0;
        Serial.println("[button1]按钮已弹起=0");

      }
    }
  }
//按钮3
  if(button3_zt==0){
    if(data.button3==LOW){//拉低
        button3_zt=1;
        //Serial.println("[button3]按钮被按下=1");
    }
  }
  if(button3_zt==1){
    if(data.button3==HIGH){//拉高

        button3_zt=0;
        Serial.println("[button3]按钮已弹起=0");
        //以下是按钮3处理事件，主要用于屏幕切换
        screen += 1;
        if(screen>3){screen=1;};

    }
  }
}
void oled(){
  u8g2.firstPage();  //一下是显示实现部分
  do {
  u8g2.setFont(u8g2_font_ncenB08_tr);

  if(screen==1){//第1屏UI
      u8g2.setCursor(0, y[1]);
      u8g2.print("X1");
      u8g2.drawFrame(20, -7+y[1], 84, 6);//  绘制一个框架（空框）,x,y起点坐标；w框的宽度；h框的高度
      u8g2.drawBox(22,-5+y[1],float(data.j1PotX)/255*80,2);//绘制比例进度条,长度为78px
      u8g2.setCursor(110, y[1]);
      u8g2.print(data.j1PotX);

      u8g2.setCursor(0, y[2]);
      u8g2.print("Y1");
      u8g2.drawFrame(20, -7+y[2], 84, 6);
      u8g2.drawBox(22,-5+y[2],float(data.j1PotY)/255*80,2);
      u8g2.setCursor(110, y[2]);
      u8g2.print(data.j1PotY);

      u8g2.setCursor(0, y[3]);
      u8g2.print("X2");
      u8g2.drawFrame(20, -7+y[3], 84, 6);
      u8g2.drawBox(22,-5+y[3],float(data.j2PotX)/255*80,2);
      u8g2.setCursor(110, y[3]);
      u8g2.print(data.j2PotX);
      u8g2.setCursor(0, y[4]);
      u8g2.print("Y2");
      u8g2.drawFrame(20, -7+y[4], 84, 6);
      u8g2.drawBox(22,-5+y[4],float(data.j2PotY)/255*80,2);
      u8g2.setCursor(110, y[4]);
      u8g2.print(data.j2PotY);

      u8g2.setCursor(0,y[5]);
      u8g2.print("S1:");
      if(data.tSwitch1==0){
        u8g2.print("On");
        Serial.print("X1:");
        Serial.print(data.j1PotX);
        Serial.print("/");
        Serial.print(analogRead(A1));
        Serial.print(" Y1:");
        Serial.print(data.j1PotY);
        Serial.print("/");
        Serial.print(analogRead(A0));
        Serial.println("");
      }else{
        u8g2.print("Off");
      }
      u8g2.setCursor(40,y[5]);
      u8g2.print("/ S2:");
      if(data.tSwitch2==0){
        u8g2.print("On");
      }else{
        u8g2.print("Off");
      }

  }else if(screen==2){//第2屏UI

      u8g2.setCursor(0, y[1]);
      u8g2.print("P1");
      u8g2.drawFrame(20, -7+y[1], 84, 6);//  绘制一个框架（空框）,x,y起点坐标；w框的宽度；h框的高度
      u8g2.drawBox(22,-5+y[1],float(data.pot1)/255*80,2);//绘制比例进度条,长度为78px
      u8g2.setCursor(110, y[1]);
      u8g2.print(data.pot1);

      u8g2.setCursor(0, y[2]);
      u8g2.print("P2");
      u8g2.drawFrame(20, -7+y[2], 84, 6);//  绘制一个框架（空框）,x,y起点坐标；w框的宽度；h框的高度
      u8g2.drawBox(22,-5+y[2],float(data.pot2)/255*80,2);//绘制比例进度条,长度为78px
      u8g2.setCursor(110, y[2]);
      u8g2.print(data.pot2);

      u8g2.setCursor(0, y[3]);
      u8g2.print("Ro");
      u8g2.drawFrame(20, -7+y[3], 84, 6);//  绘制一个框架（空框）,x,y起点坐标；w框的宽度；h框的高度
      u8g2.drawBox(22,-5+y[3],float(roll)/255*80,2);//绘制比例进度条,长度为78px
      u8g2.setCursor(110, y[3]);
      u8g2.print(roll);

      u8g2.setCursor(0, y[4]);
      u8g2.print("Pi");
      u8g2.drawFrame(20, -7+y[4], 84, 6);//  绘制一个框架（空框）,x,y起点坐标；w框的宽度；h框的高度
      u8g2.drawBox(22,-5+y[4],float(pitch)/255*80,2);//绘制比例进度条,长度为78px
      u8g2.setCursor(110, y[4]);
      u8g2.print(pitch);

      u8g2.setCursor(0,y[5]);
      u8g2.print("Vcc:4.7V");

  }else if(screen==3){//第3屏UI
      u8g2.setCursor(0, y[1]);
      u8g2.print("Vcc:");
      u8g2.print("4.7V");
      u8g2.setCursor(64, y[1]);
      u8g2.print("Vcc:");
      u8g2.print("4.7V");

      u8g2.setCursor(0, y[2]);
      u8g2.print("Sw1:");
      u8g2.print("320");
      u8g2.setCursor(64, y[2]);
      u8g2.print("Sw2:");
      u8g2.print("5.2");

      u8g2.setCursor(0, y[3]);
      u8g2.print("Sw1:");
      u8g2.print("320");
      u8g2.setCursor(64, y[3]);
      u8g2.print("Sw2:");
      u8g2.print("5.2");

      u8g2.setCursor(0, y[4]);
      u8g2.print("Sw1:");
      u8g2.print("320");
      u8g2.setCursor(64, y[4]);
      u8g2.print("Sw2:");
      u8g2.print("5.2");

      u8g2.setCursor(0,y[5]);
      u8g2.print("VCC:");
      u8g2.print("3.8v");

  }
  /*
  u8g2.setCursor(85,y[5]);
  u8g2.print("SW2:");
  if(data.tSwitch2==0){
    u8g2.print("On");
  }else{
    u8g2.print("Off");
  }*/
  //画屏幕底部点点导航
  if(screen==1){
    u8g2.drawDisc(100,y[5]-4,3);
  }else{
    u8g2.drawCircle(100,y[5]-4,3);
  }
  if(screen==2){
    u8g2.drawDisc(110,y[5]-4,3);
  }else{
    u8g2.drawCircle(110,y[5]-4,3);
  }
  if(screen==3){
    u8g2.drawDisc(120,y[5]-4,3);
  }else{
    u8g2.drawCircle(120,y[5]-4,3);
  }

  } while( u8g2.nextPage() );
  delay(1);//显示的时间间隔。
}



void initialize_MPU6050() {
  Wire.begin();                      // Initialize comunication
  Wire.beginTransmission(MPU);       // Start communication with MPU6050 // MPU=0x68
  Wire.write(0x6B);                  // Talk to the register 6B
  Wire.write(0x00);                  // Make reset - place a 0 into the 6B register
  Wire.endTransmission(true);        //end the transmission
  // Configure Accelerometer
  Wire.beginTransmission(MPU);
  Wire.write(0x1C);                  //Talk to the ACCEL_CONFIG register
  Wire.write(0x10);                  //Set the register bits as 00010000 (+/- 8g full scale range)
  Wire.endTransmission(true);
  // Configure Gyro
  Wire.beginTransmission(MPU);
  Wire.write(0x1B);                   // Talk to the GYRO_CONFIG register (1B hex)
  Wire.write(0x10);                   // Set the register bits as 00010000 (1000dps full scale)
  Wire.endTransmission(true);
}

void calculate_IMU_error() {
  // We can call this funtion in the setup section to calculate the accelerometer and gury data error. From here we will get the error values used in the above equations printed on the Serial Monitor.
  // Note that we should place the IMU flat in order to get the proper values, so that we then can the correct values
  // Read accelerometer values 200 times
  while (c < 200) {
    Wire.beginTransmission(MPU);
    Wire.write(0x3B);
    Wire.endTransmission(false);
    Wire.requestFrom(MPU, 6, true);
    AccX = (Wire.read() << 8 | Wire.read()) / 4096.0 ;
    AccY = (Wire.read() << 8 | Wire.read()) / 4096.0 ;
    AccZ = (Wire.read() << 8 | Wire.read()) / 4096.0 ;
    // Sum all readings
    AccErrorX = AccErrorX + ((atan((AccY) / sqrt(pow((AccX), 2) + pow((AccZ), 2))) * 180 / PI));
    AccErrorY = AccErrorY + ((atan(-1 * (AccX) / sqrt(pow((AccY), 2) + pow((AccZ), 2))) * 180 / PI));
    c++;
  }
  //Divide the sum by 200 to get the error value
  AccErrorX = AccErrorX / 200;
  AccErrorY = AccErrorY / 200;
  c = 0;
  // Read gyro values 200 times
  while (c < 200) {
    Wire.beginTransmission(MPU);
    Wire.write(0x43);
    Wire.endTransmission(false);
    Wire.requestFrom(MPU, 4, true);
    GyroX = Wire.read() << 8 | Wire.read();
    GyroY = Wire.read() << 8 | Wire.read();
    // Sum all readings
    GyroErrorX = GyroErrorX + (GyroX / 32.8);
    GyroErrorY = GyroErrorY + (GyroY / 32.8);
    c++;
  }
  //Divide the sum by 200 to get the error value
  GyroErrorX = GyroErrorX / 200;
  GyroErrorY = GyroErrorY / 200;
  // Print the error values on the Serial Monitor
  Serial.print("AccErrorX: ");
  Serial.println(AccErrorX);
  Serial.print("AccErrorY: ");
  Serial.println(AccErrorY);
  Serial.print("GyroErrorX: ");
  Serial.println(GyroErrorX);
  Serial.print("GyroErrorY: ");
  Serial.println(GyroErrorY);
}

void read_IMU() {
  // === Read acceleromter data === //
  Wire.beginTransmission(MPU);
  Wire.write(0x3B); // Start with register 0x3B (ACCEL_XOUT_H)
  Wire.endTransmission(false);
  Wire.requestFrom(MPU, 6, true); // Read 6 registers total, each axis value is stored in 2 registers
  //For a range of +-8g, we need to divide the raw values by 4096, according to the datasheet
  AccX = (Wire.read() << 8 | Wire.read()) / 4096.0; // X-axis value
  AccY = (Wire.read() << 8 | Wire.read()) / 4096.0; // Y-axis value
  AccZ = (Wire.read() << 8 | Wire.read()) / 4096.0; // Z-axis value

  // Calculating angle values using
  accAngleX = (atan(AccY / sqrt(pow(AccX, 2) + pow(AccZ, 2))) * 180 / PI) -1.17; // AccErrorX ~(-1.15) See the calculate_IMU_error()custom function for more details
  accAngleY = (atan(-1 * AccX / sqrt(pow(AccY, 2) + pow(AccZ, 2))) * 180 / PI) +0.36; // AccErrorX ~(0.5)

  // === Read gyro data === //
  previousTime = currentTime;        // Previous time is stored before the actual time read
  currentTime = millis();            // Current time actual time read
  elapsedTime = (currentTime - previousTime) / 1000;   // Divide by 1000 to get seconds
  Wire.beginTransmission(MPU);
  Wire.write(0x43); // Gyro data first register address 0x43
  Wire.endTransmission(false);
  Wire.requestFrom(MPU, 4, true); // Read 4 registers total, each axis value is stored in 2 registers
  GyroX = (Wire.read() << 8 | Wire.read()) / 32.8; // For a 1000dps range we have to divide first the raw value by 32.8, according to the datasheet
  GyroY = (Wire.read() << 8 | Wire.read()) / 32.8;
  GyroX = GyroX +5.34; //// GyroErrorX ~(-1.85)
  GyroY = GyroY +3.65; // GyroErrorY ~(0.15)
  // Currently the raw values are in degrees per seconds, deg/s, so we need to multiply by sendonds (s) to get the angle in degrees
  gyroAngleX = GyroX * elapsedTime;
  gyroAngleY = GyroY * elapsedTime;

  // Complementary filter - combine acceleromter and gyro angle values
  angleX = 0.98 * (angleX + gyroAngleX) + 0.02 * accAngleX;
  angleY = 0.98 * (angleY + gyroAngleY) + 0.02 * accAngleY;
  // Map the angle values from -90deg to +90 deg into values from 0 to 255, like the values we are getting from the Joystick
  //从-90到+90的值转换为0-255的字节值
  data.j1PotX = map(angleX, -90, +90, 255, 0);
  data.j1PotY = map(angleY, -90, +90, 0, 255);


    yaw =  yaw + GyroZ * elapsedTime;
  // Complementary filter - combine acceleromter and gyro angle values
  roll = 0.96 * gyroAngleX + 0.04 * accAngleX;
  pitch = 0.96 * gyroAngleY + 0.04 * accAngleY;

  // Print the values on the serial monitor
  Serial.print(roll);
  Serial.print("/");
  Serial.print(pitch);
  Serial.print("/");
  Serial.println(yaw);

/**
  Serial.print("X1:");
  Serial.print(data.j1PotX);
  Serial.print("/");
  Serial.print(analogRead(A1));
  Serial.print(" Y1:");
  Serial.print(data.j1PotY);
  Serial.print("/");
  Serial.print(analogRead(A0));
  Serial.println("");
  */
}


//**汉字处理****************************************************************************
void HZK(int p_x,int p_y,int p_id){//显示汉字
    u8g2.drawXBMP(x[p_x], y[p_y], fontsize, fontsize,hz12[p_id]);
}
//写词语,fx=0正常/1居中,p_num>0显示编号
void XCY(int p_y,int p_id,int p_fx,int p_num){
  int x_py = 40;
  for (int i = 0; i < 4; ++i){
    if(p_num>0 & i==0){//是否显示编号
      u8g2.setCursor(x[i]+32,y[p_y]+12);
      u8g2.print(String(p_num)+".");
      x_py = x_py +8;
    }
    if(p_fx==1){//是否居中显示
      u8g2.drawXBMP(x[i]+x_py, y[p_y], fontsize, fontsize,hz12[ciyu[p_id][i]]);
    }else{
      HZK(i,p_y,ciyu[p_id][i]);
    }
  }
}
//***************************************************************************************

```
